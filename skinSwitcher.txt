/*
 *  2019, Prographer (prographerman@gmail.com) (gtaforums.com/profile/738801-prographer/)
 *
 *  SkinSwitcher 1.0
 *
 *  Описание:
 *  Мод добавляет возможность менять и покупать скины для игрока. Также вы можете использовать
 *  этот скрипт для добавления новых пунктов смены и покупки одежды на карте.
 *  Для этого подключите в ваш скрипт файл "skin_switcher.txt" командой
 *  {$INCLUDE skin_switcher.txt} и используйте опкод 05F5, передав ему 4 параметра:
 *
 *      1. (float) X-координата на карте
 *      2. (float) Y-координата на карте 
 *      3. (float) Z-координата на карте 
 *      4. (boolean) Покупаются ли скины в данной точке за деньги
 *
 *  Пример:
 *  05F5: call_scm_func @CreateSkinSwitcher params_count 4 230.9363 -1262.4741 20.1103 true
 *
 *  Установка:
 *  1. Установите Cleo v2.0.0.5.
 *  2. Поместите файлы "skin_switcher.cs", "skin_switcher.txt", "header.txt" в папку "cleo".
 *  3. Поместите файл "skin_switcher.fxt" в папку "cleo_text".
 *
 *  Приятной игры!
 */


{$CLEO}
0000:
wait 0

thread 'sknschr'
    wait 0
    
    //                                                      Sphere X    Sphere Y    Sphere Z    Commercial    
    05F5: call_scm_func @CreateSkinSwitcher params_count 4  230.9363    -1262.4741  20.1103     true
end_thread


/*
 *  Создает переключатель скинов
 *
 *  @Params: 0@ -- x-координаты сферы
 *           1@ -- y-координаты сферы
 *           2@ -- z-координаты сферы
 *           3@ -- будут ли скины покупаться за деньги (передаётся по цепочке, сначала в :CreateSkinSwitcher, а потом в :EnableSkinSwitcher)
 */
:CreateSkinSwitcher
    wait 2000
    /*
     *  0@ -- x-координаты сферы
     *  1@ -- y-координаты сферы
     *  2@ -- z-координаты сферы
     *  3@ -- будут ли скины покупаться за деньги
     *  5@ -- объект сферы
     */
    
    0169: set_fade_color 0 0 0
    
    05F5: call_scm_func @CreateSphere params_count 4   0@ 1@ 2@ 1.0   5@

    // ожидаем, пока игрок не остановится в нужных координатах    
    repeat
        wait 0
    until 00F9: player $PLAYER_CHAR stopped 1 0@ 1@ 2@ radius 1.0 1.0 1.0
    
    
    // удаляем сферу после прибытия игрока по координатам
    05F5: call_scm_func @DestroySphere params_count 1  5@
    05F5: call_scm_func @EnableSkinSwitcher params_count 1  3@ 
    
    
    // когда игрок выйдет из переключателя скинов, выключаем его
    05F5: call_scm_func @DisableSkinSwitcher params_count 0
    05F5: call_scm_func @DestroySphere params_count 1  5@

    00BE: text_clear_all

    jump @CreateSkinSwitcher
    
05F6: ret 0


/*
 *  Включает переключатель скинов
 *
 *  @Params: 0@ -- будут ли скины покупаться за деньги
 */
:EnableSkinSwitcher
    wait 0
    /*
     *  0@ -- будут ли скины покупаться за деньги
     *  1@ -- x-координаты для позиции и цели камеры
     *  2@ -- y-координаты для позиции и цели камеры
     *  3@ -- z-координаты для позиции и цели камеры
     *  4@ -- номер шага в очереди выбора скина
     *  5@ -- идентификатор текущего скина
     *  6@ -- цена нового скина
     *  7@ -- уровень розыска 
     */
    
    // 4@ -- номер шага в очереди выбора скинов. Шаг с номером "-1" -- скин, который имеет игрок перед выбором нового
    4@ = -1

    05F5: call_scm_func @GetPlayerSkinId params_count 0  5@
    
    // выведем сообщение о том, как управлять переключателем скинов
    03E5: text_box 'SKSW_2'
    
    0171: set_player $PLAYER_CHAR z_angle_to 344.0
    04C4: create_coordinate 1@ 2@ 3@ from_actor $PLAYER_ACTOR offset -0.5 3.0 0.0
    Camera.SetPosition(1@, 2@, 3@, 0.0, 0.0, 0.0)
    04C4: create_coordinate 1@ 2@ 3@ from_actor $PLAYER_ACTOR offset -0.5 0.0 0.3
    Camera.PointAt(1@, 2@, 3@, 2)
    Player.CanMove($PLAYER_CHAR) = False
    
    
    // проверяем действия игрока
    while true
        wait 0
        
        016A: fade 1 500 ms
        wait 250
        
        // выбор нужного скина
        if
            05EE: key_pressed 0xD // Enter            
        then
            05F5: call_scm_func @SetPlayerSkinByID params_count 2   5@   $PLAYER_ACTOR
            Break
        end
        
        
        // Проверка на выход из переключателя скинов
        if
            05EE: key_pressed 0x10 // Shift
        then
            if or
                // если номер выбранной одежды равен номеру скина
                003B: 4@ == 5@
                // или тому, который был изначально
                4@ == -1
            then
                // выведем сообщение о невозможности его выбрать
                03E5: text_box 'SKSW_1'
                Continue
            end
        
            018C: play_sound 13 at 1@ 2@ 3@
            05F5: call_scm_func @SetPlayerSkinByID params_count 2   4@   $PLAYER_ACTOR
            
            // проверка на то, будут ли скины покупаться за деньги
            if
                0@ == true
            then
                05F5: call_scm_func @GetPlayerSkinPrice params_count 1  4@  6@
                6@ *= -1           
                
                // проверим, есть ли у игрока нужная сумма денег
                if 
                    010A: player $PLAYER_CHAR money > 6@
                then                                       
                    0109: player $PLAYER_CHAR money -= 6@
                    //05F5: call_scm_func @SetPlayerSkinStatus params_count 2  4@  1.0
                    
                    01C0: 7@ = player $PLAYER_CHAR wanted_level
                    
                    if
                        7@ < 3
                    then
                        0110: clear_player $PLAYER_CHAR wanted_level
                    end    
                        
                else
                    // выведем сообщение о недостатке денег 
                    03E5: text_box 'SKSW_3'
                    Continue
                end
            end
            
            Break
        end            
        
        
        // Проверка выбора следующего и предыдущего скинов
        if or
            05EE: key_pressed 0x25  // левая стрелка
            05EE: key_pressed 0x41  // A button              
        then
            if 
                4@ > 0
            then
                4@ -= 1
                
                05F5: call_scm_func @GetPlayerSkinPrice params_count 1  4@  6@
                
                016A: fade 0 500 ms
                wait 500
                05F5: call_scm_func @SetPlayerSkinByID params_count 2   4@ $PLAYER_ACTOR
                05F5: call_scm_func @PrintPlayerSkinLabelById params_count 2   4@ 6@
            end        
        end
        
        if or
            05EE: key_pressed 0x27  // right arrow
            05EE: key_pressed 0x44  // D button
        then
            if 
                4@ < 11
            then
            
                4@ += 1
                
                05F5: call_scm_func @GetPlayerSkinPrice params_count 1  4@  6@
                
                016A: fade 0 500 ms
                wait 500
                05F5: call_scm_func @SetPlayerSkinByID params_count 2  4@  $PLAYER_ACTOR
                05F5: call_scm_func @PrintPlayerSkinLabelById params_count 2  4@ 6@                
            end
        end
    end
            
05F6: ret 0


/*
 *  Выключает переключатель скинов
 */
:DisableSkinSwitcher
    wait 0
    
    Player.CanMove($PLAYER_CHAR) = True
    Camera.Restore_WithJumpCut()
    Camera.SetBehindPlayer()
    
05F6: ret 0

{$INCLUDE header.txt}